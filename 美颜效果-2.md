#### 7.2.3 瘦脸滤镜

瘦脸滤镜是美颜应用的基础功能，将脸颊两侧的轮廓位置向内拉伸，以达到瘦脸的效果。应用瘦脸滤镜后，脸型更趋向于瓜子脸，如图7 - 6所示。 

瘦脸功能是脸颊位置向面部中心方向的拉伸。假设从脸颊边缘上的原点\(O\)向面部中心区域的目标点\(t\)进行拉伸，拉伸方向可以表示为\(v = t - O\)。这种拉伸强度为\(R\)，那么，以原点\(O\)为圆心，拉伸强度\(R\)为半径画一个圆，拉伸后的位置为\(t\) 。
1. **拉伸算法**

拉伸要实现的效果是圆内的像素位置都朝拉伸方向移动，并且离原点\(O\)越近，偏移的距离越大，在圆的边界上，这种拉伸的作用力为\(0\)。因此，这种拉伸关系可以表示为拉伸后的坐标\(t\) = 原点坐标\(O\) - 拉伸方向\(v×\)变形函数\(×\)变形强度\(range\)。变形函数要具备的特性是离原点越近，作用力越大，离原点越远，作用力越小，在圆的边界及边界外不受影响。

符合上述效果的变形函数有很多种，这里给出一种比较简单的、可实现瘦脸效果的变形函数。取图像内的任意一像素位置\(p\)。变形函数在代码中可表示为\((1 - \text{distance}(p, O) / \text{distance}(t, O)) * v * range\)，所以变形后的像素位置为\(O - (1 - \text{distance}(p, O) / \text{distance}(t, O)) * v * range\) ，用WebGL Shader实现的拉伸算法如下。

![image](https://github.com/user-attachments/assets/41d04ab2-6814-4f9d-890d-3144a5807a30)


```glsl
/**
 * 实现拉伸算法
 * @param uv 图像上的像素点坐标
 * @param originPoint 预设的原点坐标
 * @param targetPoint 预设的目标点坐标
 * @param range 拉伸强度
 */
vec2 shrink(vec2 uv, vec2 originPoint, vec2 targetPoint, float range) {
    vec2 direction = originPoint - targetPoint;
    float dist = distance(uv, targetPoint);
    vec2 point = targetPoint + smoothstep(0., 1., dist / range) * direction;
    return uv - originPoint + point;
}
```

2. **瘦脸功能**
   
已知拉伸的原点、目标点和拉伸强度，可计算出图像内面部区域像素点拉伸后的位置，常见的瘦脸功能有手动调整和自动美化。

    - **手动调整**（在常见的可交互式编辑工具中）：对人脸两颊及下巴进行手动拉伸以达到瘦脸效果。例如，从某原点\(O\)向目标点\(t\)拉伸后松手，此时的初始位置即拉伸算法中的原点\(O\)，停留位置即目标位置\(t\)，通过拉伸强度\(range\)控制瘦脸的强度。
   
    - **自动美化**：需要结合人脸识别，检测出面部关键点的位置信息，根据面部关键点构建出几组原点与目标点的拉伸组合。假设已有一个能够检测人脸72个关键点的SDK，如图7 - 8所示，分别取面部第2、3、4、5、6、7、8、9、10关键点作为原点，取第57关键点作为目标点，这样就构成了(原点\(O\),目标点\(t\))分别为\((2,57)\)、\((3,57)\)、\((4,57)\)、\((5,57)\)、\((6,57)\)、\((7,57)\)、\((8,57)\)、\((9,57)\)、\((10,57)\)的9个拉伸组合。

```javascript
/**
 * 生成9组(原点O, 目标点t)的组合
 * @params landmarks 包含关键点坐标信息的数组，将关键点的x、y坐标按照索引顺序排列，形如[x0, y0, x1, y1, x2, y2, ..., x71, y71]
 * @params w 输入图像的宽度
 * @params h 输入图像的高度
 */
function genPoints(landmarks, w, h) {
    const keyPoints = [
        landmarks[2 * 2] / w, 1 - landmarks[2 * 2 + 1] / h,
        landmarks[3 * 2] / w, 1 - landmarks[3 * 2 + 1] / h,
        landmarks[4 * 2] / w, 1 - landmarks[4 * 2 + 1] / h,
        landmarks[5 * 2] / w, 1 - landmarks[5 * 2 + 1] / h,
        landmarks[6 * 2] / w, 1 - landmarks[6 * 2 + 1] / h,
        landmarks[7 * 2] / w, 1 - landmarks[7 * 2 + 1] / h,
        landmarks[8 * 2] / w, 1 - landmarks[8 * 2 + 1] / h,
        landmarks[9 * 2] / w, 1 - landmarks[9 * 2 + 1] / h,
        landmarks[10 * 2] / w, 1 - landmarks[10 * 2 + 1] / h
    ];
    return keyPoints;
}
```

根据已有的关键点检测结果，得到构建的拉伸组合，就能实现自动瘦脸的滤镜，参考WebGL Shader代码如下。

```glsl
precision mediump float;
varying vec2 v_texCoord;
uniform sampler2D u_texture;
uniform float u_range;
uniform float u_strength;
uniform vec2 u_facePoint[9];

void main () {
    vec2 faceIndexs[9];
    float u_strength_x = u_strength;
    float u_strength_y = u_strength / float(2.0);
    faceIndexs[0] = vec2(u_strength_x, u_strength_y);
    faceIndexs[1] = vec2(u_strength_x, u_strength_y);
    faceIndexs[2] = vec2(u_strength_x, u_strength_y);
    faceIndexs[3] = vec2(u_strength_x, u_strength_y);
    faceIndexs[4] = vec2(0., u_strength_y);
    faceIndexs[5] = vec2(-u_strength_x, u_strength_y);
    faceIndexs[6] = vec2(-u_strength_x, u_strength_y);
    faceIndexs[7] = vec2(-u_strength_x, u_strength_y);
    faceIndexs[8] = vec2(-u_strength_x, u_strength_y);
    vec2 texCoord = v_texCoord;
    for (int i = 0; i < 9; i++) {
        vec2 point = u_facePoint[i];
        // shrink参考拉伸算法的实现代码
        texCoord = shrink(texCoord, point, faceIndexs[i], u_range);
    }
    gl_FragColor = texture2D(u_texture, texCoord);
}
```

#### 7.2.4 大眼滤镜

大眼滤镜作为美颜的基础功能之一，已经成了美颜滤镜的默认选项。应用大眼滤镜后的效果如图7 - 9所示。 

大眼是将眼部区域向外扩大。与瘦脸滤镜算法类似，定义这种缩放同样需要设定缩放原点\(O\)与拉伸强度\(range\)。在图7 - 10中，选择眼部中心原点\(O\)为圆心，缩放半径为\(R\)，对圆内的任意一点\(m\)，在缩放因子\(k\)的作用下向外扩大至点\(n\) 。

1. **缩放算法**

眼部放大后的坐标可表示为\(t\) = 原点坐标\(O\) + 拉伸方向\(v×\)缩放因子\(k\)，这里同样给出一种比较简单的、可实现大眼效果的缩放因子实现。
    - 眼部区域内取点\(p\)与原点\(O\)的距离在代码中表示为\(\text{dist} = \text{distance}(p, O)\)。
    - 缩放因子在代码中表示为\(k = 1.0 - (1.0 - \text{dist2} / \text{radius2}) * \text{range}\)，其中缩放因子取值为\((0, 1]\)。

输入眼部的中心坐标（\(\text{center}\)）、圆的半径（\(\text{radius}\)）、缩放强度（\(\text{range}\)），可实现以下缩放算法，计算出眼部区域坐标\(\text{uv}\)放大之后的像素位置。
```glsl
/**
 * 实现缩放算法
 * @param uv 图像上的像素点坐标
 * @param center 预设的中心点坐标
 * @param radius 预设的圆半径
 * @param range 拉伸强度
 */
vec2 inflate(vec2 uv, vec2 center, float radius, float range){
    float dist = distance(uv, center);
    vec2 direction = normalize(uv - center);
    float scale = 1 - (1 - smoothstep(0., 1., dist / radius)) * range;
    float newPos = dist * scale;
    return center + newPos * direction;
}
```
2. **大眼功能**

![image](https://github.com/user-attachments/assets/ab259103-ee91-4c5b-82ef-e10ebc4dd982)


与瘦脸滤镜一样，大眼滤镜也分为手动调整和自动美化两种，有了缩放算法，无论是手动调整还是自动美化，其关键都在于计算原点坐标、半径和缩放强度。
  
- **手动调整**（在常见的可交互式编辑工具中）：在手动调整模式下，用户从眼部区域的某个位置移向另一个位置，初始位置即拉伸算法中的原点\(O\)，停留位置\(t\)与原点\(O\)的距离为圆的半径（\(\text{radius}\)），通过缩放强度（\(\text{range}\)）控制大眼的强度。 
    
- **自动美化**：同样需要结合人脸识别，检测出眼部关键点的位置信息。以如图7 - 8所示的人脸SDK提供的人脸72个关键点信息为例，分别取第71关键点和第72关键点作为左眼和右眼的中心点，取第61关键点和第64关键点距离的一半作为圆的半径（\(\text{radius}\)），缩放强度（\(\text{range}\)）可根据效果调节，默认为\(1\)。

```javascript
/**
 * 分别计算左眼中心点、右眼中心点、缩放半径
 * @params landmarks 人脸关键点检测SDK识别出的72个关键点坐标信息
 * @params w 输入图像的宽度
 * @params h 输入图像的高度
 */
function genPoints(landmarks, w, h) {
    const leftEyeCenterPos = getCoord(landmarks, w, h, 71);
    const rightEyeCenterPos = getCoord(landmarks, w, h, 72);
    const radius = (getCoord(landmarks, w, h, 64)[1] - getCoord(landmarks, w, h, 61)[1]) / 2;
}
```


有了原点（\(\text{center}\)）、圆的半径（\(\text{radius}\)）和缩放强度（\(\text{range}\)）的计算规则，结合缩放算法，实现大眼滤镜的WebGL Shader代码如下。

```glsl
precision mediump float;
// 图像坐标
varying vec2 v_texCoord;
// 图像纹理
uniform sampler2D u_texture;
// 左右眼的坐标点
uniform vec2 u_leftEyeCenterPos;
uniform vec2 u_rightEyeCenterPos;
// 作用范围
uniform float u_radius;
// 作用强度
uniform float u_range;

void main () {
    // inflate参考缩放算法的实现
    vec2 t1 = inflate(v_texCoord, u_leftEyeCenterPos, u_radius, u_range);
    vec2 t2 = inflate(t1, u_rightEyeCenterPos, u_radius, u_range);
    gl_FragColor = texture2D(u_texture, t2);
}
```

#### 7.3 总结

本章介绍了一些常见的滤镜实现。简单滤镜主要包括灰度和色相旋转，分别介绍了两个滤镜的CSS函数实现及WebGL Shader实现。美颜效果主要包括最基本的美白滤镜、磨皮滤镜、瘦脸滤镜和大眼滤镜。美白滤镜算法通过LUT实现，磨皮滤镜算法通过双边滤波器实现。瘦脸滤镜和大眼滤镜根据拉伸和缩放算法实现变形效果。 

到此为止，本书介绍了模型引入、模型前处理、模型后处理和图像处理等内容。读者可以运用所学的知识定制化地打造Web AI的基础技术框架，以满足大部分的业务需求。 
